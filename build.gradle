plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta12'
}

group = 'ru.mrbedrockpy'
def artifact = 'BedrockLib'
version = '3.3.3'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

def lombok = "org.projectlombok:lombok:1.18.38"
dependencies {
    compileOnly "org.jetbrains:annotations:24.0.1"
    compileOnly "org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT"
    compileOnly lombok
    annotationProcessor lombok
}

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    configurations = project.configurations.named('runtimeClasspath').map { [it] }
    archiveFileName = "${project.name}-${project.version}.jar"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = artifact
            version = project.version
            from components.java
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
